plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.firebase.crashlytics'
    id 'com.google.gms.google-services'
}

android {
    namespace 'com.solo4.millionerquiz'
    compileSdk 33

    defaultConfig {
        applicationId "com.solo4.millionerquiz"
        minSdk 27
        targetSdk 33
        versionCode 4
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        create("release") {
            def secureProps = new Properties()
            if (file("../secure.properties").exists()) {
                secureProps.load(new FileInputStream(file("../secure.properties")))
            } else {
                throw GradleException("secure.properties is not exist. It must be in root project dir")
            }

            def password = secureProps.getProperty("KEYSTORE_PASSWORD")
            def alias = secureProps.getProperty("KEYSTORE_ALIAS")

            if (password.isBlank() || alias.isBlank()) {
                throw GradleException("Can't create release build because of empty password or alias data")
            }

            storeFile = file("release-key.jks")
            storePassword = password
            keyPassword = password
            keyAlias = alias
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.getByName("release")
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", "ad_banner_id", "\"ca-app-pub-2425331811935876/4874112919\""
            buildConfigField "String", "ad_interstitial_id", "\"ca-app-pub-2425331811935876/7997053982\""
            buildConfigField "boolean", "showAd", "true"
        }
        debug {
            buildConfigField "String", "ad_banner_id", "\"ca-app-pub-3940256099942544/6300978111\""
            buildConfigField "String", "ad_interstitial_id", "\"ca-app-pub-3940256099942544/1033173712\""
            buildConfigField "boolean", "showAd", "true"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        buildConfig = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    flavorDimensions += "application"
    productFlavors {
        create("full") {
            dimension = "application"
            versionNameSuffix = "-full"

            buildConfigField "boolean", "isFullApp", "true"
        }
        create("createquestions") {
            dimension = "application"
            applicationIdSuffix = ".createquestions"
            versionNameSuffix = "-createquestions"

            buildConfigField "boolean", "isFullApp", "false"
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'

    implementation 'androidx.core:core-splashscreen:1.0.1'

    implementation 'androidx.navigation:navigation-compose:2.6.0'

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.0')
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'

    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    implementation 'io.insert-koin:koin-androidx-compose:3.4.5'
    implementation 'io.insert-koin:koin-androidx-compose-navigation:3.4.5'

    implementation 'io.coil-kt:coil-compose:2.4.0'

    implementation 'com.google.code.gson:gson:2.10.1'

    implementation platform('com.google.firebase:firebase-bom:32.1.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // ads
    implementation 'com.google.android.gms:play-services-ads:22.2.0'
}